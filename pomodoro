#!/bin/zsh

SLEEP_DURATION=1

# Старт таймера
start-pomodoro() {
    say "Start ${task}"
    echo "Task \"${task}\" - ${mins} min."
    # Выключаем уведомления MacOS
    osascript -e "tell application \"System Events\" to keystroke \"N\" using {command down, shift down, option down, control down}"
    # Включаем музыку
    osascript -e "tell application \"Music\" to play playlist \"НТР\""
}

# Остановка таймера
stop-pomodoro() {
    local checkbox="[v]"
    local phrase="You are rock!"

    end=$SECONDS
    duration=$(((end - start) / 60))
    if ((duration < mins)); then
        checkbox="[ ]"
        phrase="Fail"
    fi

    # Записываем результат
    echo "${now} | ${checkbox} | ${task} | ${duration} min." >>done.log
    # Включаем уведомления MacOS
    osascript -e "tell application \"System Events\" to keystroke \"N\" using {command down, shift down, option down, control down}"
    # Выключаем музыку
    osascript -e "tell application \"Music\" to stop"
    # Говорим фразу
    say "${phrase}"
}

progress-bar() {
    local duration
    local columns
    local space_available
    local fit_to_screen
    local space_reserved

    space_reserved=10
    duration=${1}
    columns=$(tput cols)
    space_available=$((columns - space_reserved))

    if ((duration < space_available)); then
        fit_to_screen=1
    else
        fit_to_screen=$((duration / space_available))
        fit_to_screen=$((fit_to_screen + 1))
    fi

    already_done() { for ((done = 0; done < (elapsed / fit_to_screen); done = done + 1)); do printf "▇"; done; }
    remaining() { for ((remain = (elapsed / fit_to_screen); remain < (duration / fit_to_screen); remain = remain + 1)); do printf " "; done; }
    percentage() { printf "| %s%%  " $((((elapsed) * 100) / (duration) * 100 / 100)); }
    clean_line() { printf "\r"; }

    for ((elapsed = 1; elapsed <= duration; elapsed = elapsed + 1)); do
        columns=$(tput cols)
        space_available=$((columns - space_reserved))
        already_done
        remaining
        percentage
        sleep "$SLEEP_DURATION"
        clean_line
    done
    clean_line
}

mins=${1}
task=${2}
now=$(date +"%Y-%m-%d %T")
start=$SECONDS

function TRAPINT() {
    stop-pomodoro
    exit
}

# Очищаем терминал перед запуском прогрессбара
reset

start-pomodoro
progress-bar $((mins * 60))
stop-pomodoro
